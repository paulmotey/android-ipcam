buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "cz.janchvala.android.ipcamera"
        testApplicationId "cz.janchvala.android.ipcamera.test"

        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    /**
     * We use only default (debug, release) build types for now. Debug BT is set not to obfuscate code
     */
    buildTypes {
        debug {
            /** we need to enable minify in order to get shrinkResources working */
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-debug.txt'
        }

        release {
            /** we need to enable minify in order to get shrinkResources working */
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    /**
     * compiling against Java7 (we get some benefits as multiCatch,..)
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /**
     * pick first resource when packaging
     */
    packagingOptions {
        pickFirst 'META-INF/notice.txt'
        pickFirst 'META-INF/license.txt'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/NOTICE'
    }
}

/**
 * APT plugin for AndroidAnnotations pre processing
 */
apt {
    arguments {
        androidManifestFile variant.outputs[0].variantOutputData.processResourcesTask.manifestFile
        // If you're using Android NBS flavors you should use the following line instead of hard-coded packageName
        resourcePackageName android.defaultConfig.applicationId

        trace true
        logLevel 'DEBUG'
    }
}


dependencies {
    /**
     * this project depends on libstreaming module
     */
    compile project(':libstreaming')

    /**
     * include libraries in libs folder
     */
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /**
     * AndroidAnnotations pre processing library provides annotations for "Code on diet" development.
     */
    apt "org.androidannotations:androidannotations:3.2"
    compile 'org.androidannotations:androidannotations-api:3.2'

    /**
     * We also use the @Rest annotations for Rest communication so spring-android is needed too.
     * Jackson is used as payload (de)serializer.
     */
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile "com.fasterxml.jackson.core:jackson-databind:2.4.2"

    /**
     * material design FloatingActionButton
     */
    compile 'com.melnykov:floatingactionbutton:1.0.6'

    /**
     * support android annotations provides useful annotations for handling resources and nulls
     */
    compile 'com.android.support:support-annotations:21.0.0'

    /**
     * Support (compatibility)
     */
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:palette-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:gridlayout-v7:21.0.0'

    /**
     * Google Guava utility library
     */
    compile 'com.google.guava:guava:18.0'
}
